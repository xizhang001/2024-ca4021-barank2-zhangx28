-- Run commands before ativating hive
-- rm -r metastore_db/
-- $HIVE_HOME/bin/schematool -dbType derby -initSchema

------------------------------------------------------------------
-- Create database for project
CREATE DATABASE IF NOT EXISTS PALO_ALTO;

-- Specify the database to use
USE PALO_ALTO;

------------------------------------------------------------------
-- Remove tables if exist
DROP TABLE IF EXISTS full_table;

------------------------------------------------------------------
-- Create table full_table
CREATE TABLE IF NOT EXISTS full_table(
Station_Name string, Start_Date string, Charging_Time string, Energy double, Station_Cluster_Name string)
ROW FORMAT DELIMITED
FIELDS TERMINATED BY ',';

------------------------------------------------------------------
-- Load data into full_table
LOAD DATA LOCAL INPATH './tests/data/data_for_queries/*' INTO TABLE full_table;

------------------------------------------------------------------
-- 1. Station Row Count Query
INSERT OVERWRITE LOCAL DIRECTORY './tests/data/queries_results/charges_per_station' 
ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' 
SELECT Station_Name, COUNT(Station_Name) AS Count_Station
FROM full_table
GROUP BY Station_Name
ORDER BY Count_Station DESC;

------------------------------------------------------------------
-- 2. Station Cluster Row Count Query
INSERT OVERWRITE LOCAL DIRECTORY './tests/data/queries_results/charges_per_station_cluster' 
ROW FORMAT DELIMITED FIELDS TERMINATED BY ','
SELECT Station_Cluster_Name, COUNT(Station_Cluster_Name) AS Count_Station
FROM full_table
GROUP BY Station_Cluster_Name
ORDER BY Count_Station DESC;

------------------------------------------------------------------
-- 3. Station Cluster Start and End Time Query
INSERT OVERWRITE LOCAL DIRECTORY './tests/data/queries_results/station_cluster_time_range' 
ROW FORMAT DELIMITED FIELDS TERMINATED BY ','
SELECT Station_Cluster_Name,
from_unixtime(MIN(unix_timestamp(Start_Date ,"dd/MM/yyyy HH:mm")), "dd/MM/yyyy HH:mm") AS Start_Time,
from_unixtime(MAX(unix_timestamp(Charging_Time,"HH:mm:ss")+unix_timestamp(Start_Date ,"dd/MM/yyyy HH:mm")), "dd/MM/yyyy HH:mm") AS End_Time
FROM full_table
GROUP BY Station_Cluster_Name
ORDER BY Station_Cluster_Name DESC;